ROUTINE ARYARC [Type=INT]
ARYARC ; PRINT BUILD ARRAY FOR ARCHIVAL LAB [ 02/14/2002   4:56 PM ]
 ; INTERPATH - RH
 ; 8/24/89
 
START S $ZT="ERROR^ARYARC",DOCHK="NO" K C,CMT,R,TST,RLAB,REQ
 N (AG,AGSX,AMB,BDT,C,CMT,DC,DNA,DNAO,DNO,DO,DOB,DOC,DR,DT,HNO,HRS,LOC,MS,ONO,PC,PNA,PNO,PRN,R,REFLAB,REQ,RTYP,SA,SPIN,SSN,SX,TC,TO,TR,TST,TZ,C1,P1,P2)
 ;
PAT S LEV="" F  S LEV=$O(^LA(PNO,LEV)) Q:LEV=""  Q:LEV>.99  S PAT(LEV)=^(LEV) I LEV=.8 S RDAT=+PAT(LEV)
 S:'$D(DNO) DNO=$Piece(PAT(.02),"#",1) S PAT(.05)=$G(PAT(.05))
 S MS=$P(PAT(.02),"#",6),TZ=$P(PAT(.02),"#",7)
 D RPRT
 ;	
TST S LEV=.99 F  S LEV=$O(^LA(PNO,LEV)) Q:LEV=""  D
 .S DATA=^LA(PNO,LEV) I $D(^FLG("NOPRNT",LEV)) S R=$P(DATA,"*",2) I R=""!(R?." ")!(R=0)!(R="0.0") Q
 .S XT=LEV#100,TST(LEV)=DATA Q
 ;
REQ S RTYP="COMPLETED" S REQ=^LA(PNO,.03)
RNO S LEV="" F  S LEV=$O(^LA(PNO,.03,LEV)) Q:LEV=""  D
 .I $D(^FLG("NOPRNT",LEV)) Q  ; Skip NOPRNT tests
 .S R(LEV)=^LA(PNO,.03,LEV)
DEF I $$^LIB0033(PNO)
 S PNA=$E(PAT(.01),1,28),DOC=PAT(.02),HNO=$P(PAT(.04),"^",1),SPIN=PAT(.05),AGSX=PAT(.06),LOC=PAT(.07),HRS=PAT(.08),AMB=PAT(.1),DTO=$P(PAT(.5),"*",1),DNO=$Piece(DOC,"#",1)
 S AG=$P(AGSX,"*",1),SX=$P(AGSX,"*",2),DOB=$P(AGSX,"*",3),SA=SX_AMB,VAR(1)=AGSX,DUD=$$^EXT031(.VAR),AGSX=VAR(2),BDT=$P(AGSX," ",1)
 S SSN=$$SSN^LIB0044($G(PAT(.11))),TMG=$G(PAT(.12)),ONO=$G(PAT(.13))
 S COL=PAT(.09),%DT=$P(COL,"^",1),%TM=$P(COL,"^",2) D ^LIB0011,^LIB0013 S DC=%DAT4,TC=%TIM I %TM="N/G" S TC=%TM
 S COM=.6000 F I=1:1 S COM=$O(PAT(COM)) Q:COM=""  Q:COM>.6999  D
 .S D=PAT(COM),CTYP=$P(D,"*",1),TNO=$P(D,"*",2),CMT=$P(D,"*",3)
 .I $D(^FLG("NOPRNT",TNO)) Q  ; Dont array NOPRNT tests
 .S CMT(CTYP,TNO,COM)=CMT
 D DCVRT,DTVRT F VAR="AG","SX","DC","TC","HRS","DO","TO","HNO","LOC" I @VAR="" S @VAR="N/G"
 G END
 ;
DCVRT S DNA=$P(DOC,"#",2),DNAO="" I DNA[":DR. " S DNAO=$P(DNA,":DR. ",2),DNA=$P(DNA,":DR. ",1)
 S:DNAO="" DNAO=DNA I DNA'[":DR." I DNA["," S DNA=$P(DNA,",",2)_" "_$P(DNA,",",1) I $E(DNA,1)=" " S DNA=$E(DNA,2,$L(DNA))
 S PC=$S(DNAO["D.M.D.":" D.M.D,",DNAO["D.O.":" D.O.",DNAO["D.C.":" D.C.",DNAO["M.D.":" M.D.",DNAO["D.D.S.":" D.D.S.",DNAO["D.P.M.":" D.P.M.",DNAO["C.N.M":" C.N.M.",DNAO["F.N.P":" F.N.P.",DNAO["P.A.C.":" P.A.C.",DNAO["D.V.M.":" D.V.M.",1:"")
 Q:PC=""  S DNAO=$P(DNAO,PC,1) Q
 ;
DTVRT S (DO,DR)="",%DT=$P(DTO,",",1) D ^LIB0013 S DO=%DAT4 I $D(PAT(.8)) S %DT=$P(PAT(.8),",",1) D ^LIB0013 S DR=%DAT4
 S (TO,TR)="",%TM=$P(DTO,",",2) D ^LIB0011 S TO=%TIM I $D(PAT(.8)) S %TM=$P(PAT(.8),",",2) D ^LIB0011 S TR=%TIM
 Q
 ;
RPRT S (P1,P2,C1)="",DATA=$P($G(PAT(.7)),"*",1)
 I DATA'="" S %DT=$P(DATA,",",1),%TM=$P(DATA,",",2) D ^LIB0011,^LIB0013 S P1=%DAT4_"  "_%TIM2
 S DATA=$P($G(PAT(.7)),"*",2) I DATA'="" S %DT=$P(DATA,",",1),%TM=$P(DATA,",",2) D ^LIB0011,^LIB0013 S P2=%DAT4_" "_%TIM2
 S DATA=$G(PAT(.8)) I DATA'="" S %DT=$P(DATA,",",1),%TM=$P(DATA,",",2) D ^LIB0011,^LIB0013 S C1=%DAT4_" "_%TIM2
 Q
 ;
END ; END OF ROUTINE
 D ^LIB0130(PNO)
 Q
 ;
ERROR D ^LIB0015("ARYARC") Q
